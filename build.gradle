import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'java-library'
    id 'net.ltgt.errorprone'        version '2.0.2'
    id 'com.github.spotbugs'        version '4.8.0'
    id 'nebula.release'             version '15.3.1'
}

group = 'com.pega.gcs'
description = "FringeUtils-Common"

apply from: "$rootDir/gradle/artifacts.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/pmd.gradle"
apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/spotbugs.gradle"
apply from: "$rootDir/gradle/publishing.gradle"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    errorprone("com.google.errorprone:error_prone_core:2.10.0")
    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")

    api 'com.esotericsoftware:kryo:5.2.1'
    api 'commons-io:commons-io:2.11.0'
    api 'commons-fileupload:commons-fileupload:1.4'
    api ('org.dom4j:dom4j:2.1.3') {
        exclude group: 'xpp3', module: 'xpp3'
    }
    api 'org.lz4:lz4-java:1.8.0'
    api 'org.apache.commons:commons-csv:1.9.0'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'org.apache.commons:commons-text:1.9'
    api 'org.apache.commons:commons-compress:1.21'
    api 'org.tukaani:xz:1.9'
    
    api 'org.apache.logging.log4j:log4j-api:2.17.1'
    api 'org.apache.logging.log4j:log4j-core:2.17.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:-options']
    options.compilerArgs += ['-proc:none']
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    // configure error prone
    options.errorprone.check("UnnecessaryParentheses", CheckSeverity.OFF)
}

tasks.withType(Javadoc) {
    failOnError = false
}

tasks.withType (Test) {
    maxParallelForks = 2

    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/${name}.exec")
        classDumpDir = file("$buildDir/jacoco/${name}_ClassDump")
    }
}

task copyLicense(type: Copy) {
    from "LICENSE"
    into "$buildDir"
}

build.finalizedBy jacocoTestReport, jacocoTestCoverageVerification